; ******************************************************************************
; ******************************************************************************
;
;		Name : 		memory.voc
;		Purpose : 	Memory Access Functions
;		Author : 	Paul Robson (paul@robsons.org.uk)
;		Created : 	1st October 2019
;
; ******************************************************************************
; ******************************************************************************

; ******************************************************************************
;
;							 Memory - Read a byte
;
; ******************************************************************************

Memory_ReadByte:	;; [c@]
		sta 	zTemp0 						; save address
		sty 	zTemp0+1
		lda 	(zTemp0) 					; load byte in.
		ldy 	#0
		rts

; ******************************************************************************
;
;							 Memory - Read a word
;
; ******************************************************************************

Memory_ReadWord:	;; [@]
		sta 	zTemp0 						; save address
		sty 	zTemp0+1
		ldy 	#1							; get high byte
		lda 	(zTemp0),y
		tay
		lda 	(zTemp0) 					; load low byte in.
		rts		

; ******************************************************************************
;
;							 Memory - Write a byte
;
; ******************************************************************************

Memory_WriteByte:	;; [c!]
		sta 	zTemp0 						; save address
		sty 	zTemp0+1
		lda 	lStack,x 					; load byte in.
		sta 	(zTemp0)					; write it.
		.drop2 								; and drop the 2 stack values.
		rts

; ******************************************************************************
;
;							 Memory - Write a byte
;
; ******************************************************************************

Memory_WriteWord:	;; [!]
		sta 	zTemp0 						; save address
		sty 	zTemp0+1
		lda 	hStack,x 					; load high byte in.
		ldy 	#1
		sta 	(zTemp0),y					; write it.
		lda 	lStack,x 					; load low byte in.
		sta 	(zTemp0)					; write it.
		.drop2 								; and drop the 2 stack values.
		rts

; ******************************************************************************
;
;							 Memory - Add a word
;
; ******************************************************************************

Memory_AddWord:	;; [+!]
		sta 	zTemp0 						; save address
		sty 	zTemp0+1
		
		clc
		lda 	lStack,x 					; load low byte in.
		adc 	(zTemp0)					; add it
		sta 	(zTemp0)					; write it.

		ldy 	#1
		lda 	hStack,x 					; load high byte in.
		adc		(zTemp0),y					; add it
		sta 	(zTemp0),y					; write it.
		.drop2 								; and drop the 2 stack values.
		rts
